/*
* nSVScript         | Licensed under The GNU GPL-3.0
* Copyright Â© 2018  | SMAKTA IT Club - SMK Negeri 3 Kota Tangerang
*/

function _motionEstimate(clip a)
{
    global sW = a.width()
    global c = a
    try
    {
        _a = ScriptClip(a, """
            Subtitle(c, \
                  "Average Motion(YUV): " + dat \
                + "\n +-> values(Y): " + n_float(getDiffsY,3,3,1) \
                + "\n +-> values(U): " + n_float(getDiffsU,3,3,1) \
                + "\n +-> values(V): " + n_float(getDiffsV,3,3,1) \
                + "\n +-> Framerate: " + n_float(getFramerate,3,3,1) + "fps (" \
                    + String(c.FrameRateNumerator()) + "/" + String(c.FrameRateDenominator()) \
                    + ")" \
            , lsp=2, size=Round(sW - (sW * 96.8) / 100) \
            , font = "Consolas", text_color=$00FFFFFF, halo_color=$88000000)
        """)
        _b = FrameEvaluate(_a, """
            getFramerate = c.frameRate()
            getDiffsY = YDifferenceFromPrevious(c)
            getDiffsU = UDifferenceFromPrevious(c)
            getDiffsV = VDifferenceFromPrevious(c)
            getDiffs = (getDiffsY + getDiffsU + getDiffsV) / 3
            dat = getDiffs > 14 ? \
                "Aggressive" : \
            getDiffs > 3 ? \
                "Strong" : \
                "Low"
        """)
    }
    catch(err_msg)
    {
        Assert(false, "unknown error!" + String(err_msg))
    }

    return _b
}